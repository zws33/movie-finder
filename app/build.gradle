apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

def localPropertiesFile = rootProject.file("local.properties")
def localProperties = new Properties()
localProperties.load(new FileInputStream(localPropertiesFile))

def featureToggles = new Properties()
featureToggles.load(new FileInputStream(rootProject.file("featureToggles.properties")))

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "me.zwsmith.moviefinder"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField 'boolean', "KOIN_ENABLED", featureToggles.getProperty("KOIN_ENABLED")
    }
    buildTypes {
        debug {
            buildConfigField 'String', "ApiKey", localProperties.getProperty("TMDB_API_KEY")
        }
        release {
            buildConfigField 'String', "ApiKey", localProperties.getProperty("TMDB_API_KEY")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    support_version = "28.0.0-rc02"
    constraint_layout_version = "1.1.3"
    lifecycle_version = "1.1.1"
    retrofit_version = "2.4.0"
    gson_version = "2.8.5"
    dagger_version = "2.15"
    rx_java_version = "2.1.14"
    rx_android_version = "2.0.2"
    rx_relay_version = "2.0.0"
    rx_kotlin_version = "2.2.0"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //UI
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:support-media-compat:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    implementation('com.squareup.picasso:picasso:2.71828', {
        exclude group: 'com.android.support', module: 'exifinterface'
    })
    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation "android.arch.lifecycle:runtime:$lifecycle_version"
    kapt "android.arch.lifecycle:compiler:$lifecycle_version"

    //Network
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    //Serialization
    implementation "com.google.code.gson:gson:$gson_version"

    //Dependency Injection - Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation("com.google.dagger:dagger-android-support:$dagger_version", {
        exclude group: 'com.android.support'
    })
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    //Rx
    implementation "io.reactivex.rxjava2:rxjava:2.1.14"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation "com.jakewharton.rxrelay2:rxrelay:2.0.0"
    implementation "io.reactivex.rxjava2:rxkotlin:2.2.0"

    implementation "joda-time:joda-time:2.9.9"

    testImplementation 'junit:junit:4.12'
    testImplementation('io.kotlintest:kotlintest:2.0.4', {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    })
    testImplementation 'org.mockito:mockito-inline:2.7.8'
    testImplementation('com.nhaarman:mockito-kotlin:1.4.0', {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    })
    testImplementation 'org.amshove.kluent:kluent:1.31'
}
