apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def localPropertiesFile = rootProject.file("local.properties")
def localProperties = new Properties()
localProperties.load(new FileInputStream(localPropertiesFile))

def featureToggles = new Properties()
featureToggles.load(new FileInputStream(rootProject.file("featureToggles.properties")))

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "me.zwsmith.moviefinder"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'boolean', "KOIN_ENABLED", featureToggles.getProperty("KOIN_ENABLED")
    }
    buildTypes {
        debug {
            buildConfigField 'String', "ApiKey", localProperties.getProperty("TMDB_API_KEY")
        }
        release {
            buildConfigField 'String', "ApiKey", localProperties.getProperty("TMDB_API_KEY")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        getByName("main").java.srcDirs("src/main/kotlin")
        getByName("androidTest").java.srcDirs("src/androidTest/kotlin")
        getByName("test").java.srcDirs("src/test/kotlin")
    }
}

ext {
    support_version = "28.0.0"
    constraint_layout_version = "1.1.3"
    lifecycle_version = "2.2.0-alpha02"
    retrofit_version = "2.6.1"
    gson_version = "2.8.5"
    rx_java_version = "2.1.14"
    rx_android_version = "2.0.2"
    rx_relay_version = "2.0.0"
    rx_kotlin_version = "2.2.0"
    coroutines_version = "1.3.0-RC"
    koin_version = '1.0.0-RC-2'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //UI
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.media:media:1.0.1'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation "androidx.fragment:fragment-ktx:1.2.0-alpha01"
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.squareup.picasso:picasso:2.71828'

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    //Network
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:4.0.1"

    //Serialization
    implementation "com.google.code.gson:gson:$gson_version"

    //Dependency Injection - Koin
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    //Rx
    implementation "io.reactivex.rxjava2:rxjava:2.1.14"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation "com.jakewharton.rxrelay2:rxrelay:2.0.0"
    implementation "io.reactivex.rxjava2:rxkotlin:2.2.0"

    implementation "joda-time:joda-time:2.9.9"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.1'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation('io.kotlintest:kotlintest:2.0.4', {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    })
    testImplementation 'org.mockito:mockito-inline:2.7.8'
    testImplementation('com.nhaarman:mockito-kotlin:1.4.0', {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    })
    testImplementation 'org.amshove.kluent:kluent:1.31'
    androidTestImplementation 'androidx.test:core:1.2.1-alpha02'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}
